/****************************************************************************
 * Copyright 2016, Optimizely, Inc. and contributors                        *
 *                                                                          *
 * Licensed under the Apache License, Version 2.0 (the "License");          *
 * you may not use this file except in compliance with the License.         *
 * You may obtain a copy of the License at                                  *
 *                                                                          *
 *    http://www.apache.org/licenses/LICENSE-2.0                            *
 *                                                                          *
 * Unless required by applicable law or agreed to in writing, software      *
 * distributed under the License is distributed on an "AS IS" BASIS,        *
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. *
 * See the License for the specific language governing permissions and      *
 * limitations under the License.                                           *
 ***************************************************************************/

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'maven'
apply plugin: 'maven-publish'

version = version_name
group = group_id

android {
    compileSdkVersion compile_sdk_version
    buildToolsVersion build_tools_version

    defaultConfig {
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        consumerProguardFiles '../proguard-rules.txt'
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildToolsVersion build_tools_version
}

dependencies {
    api ("com.optimizely.ab:core-api:$java_core_ver") {
        exclude group: 'com.google.code.findbugs'
    }
    implementation "com.google.code.gson:gson:$gson_ver"

    compileOnly "com.noveogroup.android:android-logger:$android_logger_ver"
    compileOnly 'androidx.annotation:annotation:1.0.0'

    testImplementation "junit:junit:$junit_ver"
    testImplementation "org.mockito:mockito-core:$mockito_ver"
    testImplementation "com.noveogroup.android:android-logger:$android_logger_ver"

    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    // Set this dependency to use JUnit 4 rules
    androidTestImplementation 'androidx.test:rules:1.1.1'
    // Set this dependency to build and run Espresso tests
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
    androidTestImplementation "org.mockito:mockito-core:$mockito_ver"
    androidTestImplementation "com.crittercism.dexmaker:dexmaker:$dexmaker_ver"
    androidTestImplementation "com.crittercism.dexmaker:dexmaker-dx:$dexmaker_ver"
    androidTestImplementation "com.crittercism.dexmaker:dexmaker-mockito:$dexmaker_ver"
    androidTestImplementation "com.noveogroup.android:android-logger:$android_logger_ver"
    androidTestImplementation "com.fasterxml.jackson.core:jackson-databind:$jacksonversion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

uploadArchives {
    dependsOn = [':shared:releaseJavadocJar', ':shared:releaseSourcesJar']
    shouldRunAfter = [':shared:clean']
    repositories {
        mavenDeployer {
            repository(url: "https://api.bintray.com/maven/optimizely/optimizely/optimizely-sdk-android-shared") {
                authentication(userName: bintray_user, password: bintray_api_key)
            }
            pom.artifactId = "android-sdk-shared"
        }
    }
}

android.libraryVariants.all { variant ->
    task("${variant.name}Javadoc", type: Javadoc, dependsOn: "assemble${variant.name.capitalize()}") {
        source = variant.javaCompile.source

        title = "Optimizely X Android Shared"

        options.links("http://docs.oracle.com/javase/8/docs/api/");
        options.linksOffline("http://d.android.com/reference", "${android.sdkDirectory}/docs/reference");

        // First add all of your dependencies to the classpath, then add the android jars
        doFirst {
            classpath += files(variant.javaCompileProvider.get().classpath.files)
            classpath += files(android.getBootClasspath())
        }
        // We're excluding these generated files
        exclude '**/BuildConfig.java'
        exclude '**/R.java'


        options.tags = ["hide"]
    }
}

android.libraryVariants.all { variant ->
    task("${variant.name}JavadocJar", type: Jar, dependsOn: "${variant.name}Javadoc") {
        classifier = 'javadoc'
        from tasks["${variant.name}Javadoc"].destinationDir
    }

    // Add the Javadoc jar to the project's artifacts. This will allow us to upload it easily later
    project.artifacts.add("archives", tasks["${variant.name}JavadocJar"]);
}

android.libraryVariants.all { variant ->
    task("${variant.name}SourcesJar", type: Jar) {
        classifier = 'sources'
        from variant.javaCompile.source
    }
    project.artifacts.add("archives", tasks["${variant.name}SourcesJar"]);
}

