sourceCompatibility = 1.6
targetCompatibility = 1.6

dependencies {
    compile project(':core-api')

    provided group: 'com.google.code.findbugs', name: 'annotations', version: findbugsVersion
    provided group: 'com.google.code.findbugs', name: 'jsr305', version: findbugsVersion
    provided group: 'com.google.code.gson', name: 'gson', version: gsonVersion

    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: httpClientVersion
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
    }
}

apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    url 'https://github.com/optimizely/java-sdk'
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'optimizely'
                            name 'Optimizely'
                            email 'developers@optimizely.com'
                        }
                    }
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'optimizely'
        name = 'optimizely-sdk-httpclient'
        userOrg = 'optimizely'
        version {
            name = getVersion()
        }
        publications = ['mavenJava']
    }
}

publishArtifacts()
